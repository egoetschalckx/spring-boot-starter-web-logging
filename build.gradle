plugins {
    id 'java'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.goetschalckx'

sourceCompatibility = 1.8

version = '0.1.0-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation 'net.logstash.logback:logstash-logback-encoder:6.3'

    // dont build this in. this library is pointless without spring boot web anyways
    // this is the lowest possible release that supports the required features
    implementation 'org.springframework.boot:spring-boot-starter-web:2.0.0.RELEASE'

    testImplementation 'org.springframework.boot:spring-boot-starter-web:2.1.3.RELEASE'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.1.3.RELEASE'
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = 'Spring Boot Logging Web'
                description = 'Enables RestTemplate and RestController request logging in Spring Boot applications'
                packaging = 'jar'
                url = 'https://github.com/goetschalckx/spring-boot-logging-web'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'egoetschalckx'
                        name = 'Eric Goetschalckx'
                        email = 'eric@goetschalckx.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/goetschalckx/spring-boot-logging-web.git'
                    developerConnection = 'scm:git:https://github.com/goetschalckx/spring-boot-logging-web.git'
                    url = 'https://github.com/goetschalckx/spring-boot-logging-web'
                }
            }

            artifactId 'spring-boot-logging-web'

            from components.java
            artifact sourceJar
            artifact javadocJar
        }
    }

    repositories {
        mavenLocal()

        maven {
            def repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'

            url = version.endsWith('SNAPSHOT') ? snapshotRepositoryUrl : repositoryUrl

            println "Artifact Version: " + version
            println "Maven URL: " + url

            credentials {
                username project.hasProperty("ossrhUser") ? ossrhUser : ""
                password project.hasProperty("ossrhPassword") ? ossrhPassword : ""
            }
        }
    }

}

signing {
    sign(publishing.publications)
}

tasks.withType(Sign) {
    onlyIf {
        project.hasProperty('signing.keyId')
    }

    doFirst {
        println "Signing artifacts..."
    }
}

tasks.publish.dependsOn check
tasks.publishToMavenLocal.dependsOn check
tasks.test.finalizedBy jacocoTestReport

jacocoTestReport {
    dependsOn test

    reports {
        xml.enabled true
        csv.enabled true
        html.enabled true
    }
}
